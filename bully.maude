fmod SOUP{X :: TRIV} is
  sort Soup{X} .
  subsort X$Elt < Soup{X} .
  op empty : -> Soup{X} [ctor] .
  op __ : Soup{X} Soup{X} -> Soup{X} [ctor assoc comm id: empty] .
endfm

fmod PROC-STATE is
  sort ProcState .
  sorts Leader Normal Initiator FailedLeader .
  subsorts Leader Normal Initiator FailedLeader < ProcState .
  op leader : -> Leader [ctor] .
  op normal : -> Normal [ctor] .
  op initiator : -> Initiator [ctor] .
  op failedLeader : -> FailedLeader [ctor] .
endfm

fmod MESSAGE-TYPE is
  sort MessageType .
  sorts Election Ok Timeout .
  subsorts Election Ok Timeout < MessageType .
  op election : -> Election [ctor] .
  op ok : -> Ok [ctor] .
  op timeout : -> Timeout [ctor] .
endfm

fmod MESSAGE is
  pr NAT .
  pr MESSAGE-TYPE .
  sort Message .
  op message : Nat Nat Election -> Message [ctor] .
  op message : Nat Nat Ok -> Message [ctor] .
  op message : Nat Nat Timeout -> Message [ctor] .
endfm

view Message from TRIV to MESSAGE is
  sort Elt to Message .
endv

fmod PROC-IDS is
  pr SET{Nat} .
  vars N N0 N1 : Nat .
  vars S S0 : Set{Nat} .
  op makeBiggerThan : Nat Nat Set{Nat} -> Set{Nat} .
  op makeBiggerThanIds : Nat Set{Nat} Set{Nat} -> Set{Nat} .
  op biggerThanIds : Nat Set{Nat} -> Set{Nat} .
  op makeSmallerThan : Nat Nat Set{Nat} -> Set{Nat} .
  op makeSmallerThanIds : Nat Set{Nat} Set{Nat} -> Set{Nat} .
  op smallerThanIds : Nat Set{Nat} -> Set{Nat} .
  eq makeBiggerThan(N0, N1, S) = if N0 < N1 then insert(N1, S) else S fi .
  eq makeBiggerThanIds(N0, (N, S), S0) =
    if S == empty then makeBiggerThan(N0, N, S0) else makeBiggerThanIds(N0, S, makeBiggerThan(N0, N, S0)) fi .
  eq biggerThanIds(N, S) = makeBiggerThanIds(N, S, empty) .
  eq makeSmallerThan(N0, N1, S) = if N0 > N1 then insert(N1, S) else S fi .
  eq makeSmallerThanIds(N0, (N, S), S0) =
    if S == empty then makeSmallerThan(N0, N, S0) else makeSmallerThanIds(N0, S, makeSmallerThan(N0, N, S0)) fi .
  eq smallerThanIds(N, S) = makeSmallerThanIds(N, S, empty) .
endfm

fmod NETWORK is
  pr SET{Nat} .
  pr SOUP{Message} .
  vars FID TID : Nat .
  var IDS : Set{Nat} .
  op sndElectionMsgs : Nat Set{Nat} -> Soup{Message} .
  op sndOkMsg : Nat Nat -> Soup{Message} .
  op sndTimeoutMsg : Nat Nat -> Soup{Message} .
  eq sndElectionMsgs(FID, empty) = empty .
  eq sndElectionMsgs(FID, (TID, IDS)) = message(FID, TID, election) sndElectionMsgs(FID, IDS) .
  eq sndOkMsg(FID, TID) = message(FID, TID, ok) .
  eq sndTimeoutMsg(FID, TID) = message(FID, TID, timeout) .
endfm

fmod LABELED-EVENT is
  pr NAT .
  sort LabeledEvent .
  op notran : -> LabeledEvent .
  ops bfl bi se nee nie et iee ieo iet ibn ibl : Nat -> LabeledEvent .
endfm

fmod OCOM is
  pr NAT .
  pr PROC-STATE .
  pr NETWORK .
  pr LABELED-EVENT .
  sort OComp .
  op proc[_]:_ : Nat ProcState -> OComp [ctor] .
  op curLeader[_]:_ : Nat Nat -> OComp [ctor] .
  op sndElectionMsgCnt[_]:_ : Nat Nat -> OComp [ctor] .
  op rcvOkMsgCnt[_]:_ : Nat Nat -> OComp [ctor] .
  op rcvTimeoutMsgCnt[_]:_ : Nat Nat -> OComp [ctor] .
  op procIds:_ : Set{Nat} -> OComp [ctor] .
  op network:_ : Soup{Message} -> OComp [ctor] .
  op tran:_ : LabeledEvent -> OComp [ctor] .
endfm

view OComp from TRIV to OCOM is
  sort Elt to OComp .
endv

fmod INIT-STATE is
  pr SOUP{OComp} .
  pr OCOM .
  sort BState .
  op {_} : Soup{OComp} -> BState [ctor] .
  op init : -> BState .
  eq init = {
    (proc[0]: normal) (proc[1]: normal) (proc[2]: normal) (proc[3]: normal) (proc[4]: leader)
    (curLeader[0]: 4) (curLeader[1]: 4) (curLeader[2]: 4) (curLeader[3]: 4) (curLeader[4]: 4)
    (sndElectionMsgCnt[0]: 0) (sndElectionMsgCnt[1]: 0) (sndElectionMsgCnt[2]: 0) (sndElectionMsgCnt[3]: 0) (sndElectionMsgCnt[4]: 0)
    (rcvOkMsgCnt[0]: 0) (rcvOkMsgCnt[1]: 0) (rcvOkMsgCnt[2]: 0) (rcvOkMsgCnt[3]: 0) (rcvOkMsgCnt[4]: 0)
    (rcvTimeoutMsgCnt[0]: 0) (rcvTimeoutMsgCnt[1]: 0) (rcvTimeoutMsgCnt[2]: 0) (rcvTimeoutMsgCnt[3]: 0) (rcvTimeoutMsgCnt[4]: 0)
    (procIds: (0, 1, 2, 3, 4))
    (network: empty)
    (tran: notran)
  } .
  op init2 : -> BState .
  eq init2 = {
    (proc[0]: normal) (proc[1]: normal) (proc[2]: normal) (proc[3]: normal) (proc[4]: failedLeader)
    (curLeader[0]: 4) (curLeader[1]: 4) (curLeader[2]: 4) (curLeader[3]: 4) (curLeader[4]: 4)
    (sndElectionMsgCnt[0]: 0) (sndElectionMsgCnt[1]: 0) (sndElectionMsgCnt[2]: 0) (sndElectionMsgCnt[3]: 0) (sndElectionMsgCnt[4]: 0)
    (rcvOkMsgCnt[0]: 0) (rcvOkMsgCnt[1]: 0) (rcvOkMsgCnt[2]: 0) (rcvOkMsgCnt[3]: 0) (rcvOkMsgCnt[4]: 0)
    (rcvTimeoutMsgCnt[0]: 0) (rcvTimeoutMsgCnt[1]: 0) (rcvTimeoutMsgCnt[2]: 0) (rcvTimeoutMsgCnt[3]: 0) (rcvTimeoutMsgCnt[4]: 0)
    (procIds: (0, 1, 2, 3, 4))
    (network: empty)
    (tran: notran)
  } .
endfm

mod BULLY is
  pr PROC-IDS .
  pr INIT-STATE .

  vars S S0 S1 : Soup{OComp} .
  vars ID LID ID0 ID1 : Nat .
  vars SEC ROC RTC : Nat .
  var IDS : Set{Nat} .
  var NW : Soup{Message} .
  var E : LabeledEvent .

  op syncCurLeader : Soup{OComp} Nat Set{Nat} -> Soup{OComp} .
  eq syncCurLeader(empty, ID, IDS) = empty .
  eq syncCurLeader(S, ID, empty) = S .
  eq syncCurLeader((curLeader[ID0]: ID1) S, ID, IDS) = 
    if ID0 in IDS then (curLeader[ID0]: ID) syncCurLeader(S, ID, IDS) 
    else (curLeader[ID0]: ID1) syncCurLeader(S, ID, IDS) fi .
  eq syncCurLeader((proc[ID0]: initiator) S, ID, IDS) =
    (proc[ID0]: normal) syncCurLeader(S, ID, IDS) .
  eq syncCurLeader(S0 S1, ID, IDS) = S0 syncCurLeader(S1, ID, IDS) [owise] .

  rl [become-failed-leader] :
    {(proc[ID]: leader) (network: empty) (tran: E) S} => {(proc[ID]: failedLeader) (network: empty) (tran: bfl(ID)) S} .

  rl [become-initiator] :
    {(proc[ID]: normal) (proc[LID]: failedLeader) (curLeader[ID]: LID) 
     (sndElectionMsgCnt[ID]: SEC) (rcvOkMsgCnt[ID]: ROC) (rcvTimeoutMsgCnt[ID]: RTC) (tran: E) S} =>
    {(proc[ID]: initiator) (proc[LID]: failedLeader) (curLeader[ID]: LID)
     (sndElectionMsgCnt[ID]: 0) (rcvOkMsgCnt[ID]: 0) (rcvTimeoutMsgCnt[ID]: 0) (tran: bi(ID)) S} .

  crl [start-election] :
    {(proc[ID]: initiator) (sndElectionMsgCnt[ID]: SEC) (procIds: IDS) (network: NW) (tran: E) S} =>
    {(proc[ID]: initiator) (sndElectionMsgCnt[ID]: | biggerThanIds(ID, IDS) |) (procIds: IDS)
     (network: (sndElectionMsgs(ID, biggerThanIds(ID, IDS)) NW)) (tran: se(ID)) S}
    if SEC == 0 .

  crl [normal-execution-election] :
    {(proc[ID0]: normal) (proc[LID]: failedLeader) (curLeader[ID0]: LID)
     (sndElectionMsgCnt[ID0]: SEC) (rcvOkMsgCnt[ID0]: ROC) (rcvTimeoutMsgCnt[ID0]: RTC)
     (network: (message(ID1, ID0, election) NW)) (tran: E) S} =>
    {(proc[ID0]: initiator) (proc[LID]: failedLeader) (curLeader[ID0]: LID)
     (sndElectionMsgCnt[ID0]: 0) (rcvOkMsgCnt[ID0]: 0) (rcvTimeoutMsgCnt[ID0]: 0)
     (network: (sndOkMsg(ID0, ID1) NW)) (tran: nee(ID0)) S}
    if ID0 > ID1 .

  crl [normal-ignore-election] :
    {(proc[ID0]: normal) (curLeader[ID0]: LID) (proc[LID]: leader)
     (network: (message(ID1, ID0, election) NW)) (tran: E) S} =>
    {(proc[ID0]: normal) (curLeader[ID0]: LID) (proc[LID]: leader)
     (network: NW) (tran: nie(ID0)) S}
    if ID0 > ID1 .

  crl [election-timeout] :
    {(proc[ID0]: failedLeader) (network: (message(ID1, ID0, election) NW)) (tran: E) S} =>
    {(proc[ID0]: failedLeader) (network: (sndTimeoutMsg(ID0, ID1) NW)) (tran: et(ID0)) S}
    if ID0 > ID1 .
  
  crl [initiator-execution-election] :
    {(proc[ID0]: initiator) (network: (message(ID1, ID0, election) NW)) (tran: E) S} =>
    {(proc[ID0]: initiator) (network: (sndOkMsg(ID0, ID1) NW)) (tran: iee(ID0)) S}
    if ID0 > ID1 .

  crl [initiator-execution-ok] :
    {(proc[ID0]: initiator) (rcvOkMsgCnt[ID0]: ROC) (network: (message(ID1, ID0, ok) NW)) (tran: E) S} =>
    {(proc[ID0]: initiator) (rcvOkMsgCnt[ID0]: ROC + 1) (network: NW) (tran: ieo(ID0)) S}
    if ID0 < ID1 .

  crl [initiator-execution-timeout] :
    {(proc[ID0]: initiator) (rcvTimeoutMsgCnt[ID0]: RTC) (network: (message(ID1, ID0, timeout) NW)) (tran: E) S} =>
    {(proc[ID0]: initiator) (rcvTimeoutMsgCnt[ID0]: RTC + 1) (network: NW) (tran: iet(ID0)) S}
    if ID0 < ID1 .

  crl [initiator-become-normal] :
    {(proc[ID0]: initiator) (sndElectionMsgCnt[ID0]: SEC)
     (rcvOkMsgCnt[ID0]: ROC) (rcvTimeoutMsgCnt[ID0]: RTC) (tran: E) S} =>
    {(proc[ID0]: normal) (sndElectionMsgCnt[ID0]: SEC)
     (rcvOkMsgCnt[ID0]: ROC) (rcvTimeoutMsgCnt[ID0]: RTC) (tran: ibn(ID0)) S}
    if SEC > 0 and SEC == (ROC + RTC) and ROC > 0 .

  crl [initiator-become-leader] :
    {(proc[ID0]: initiator) (curLeader[ID0]: ID1)
     (sndElectionMsgCnt[ID0]: SEC) (rcvTimeoutMsgCnt[ID0]: RTC) (rcvOkMsgCnt[ID0]: ROC)
     (procIds: IDS) (tran: E) S} =>
    {(proc[ID0]: leader) (curLeader[ID0]: ID0)
     (sndElectionMsgCnt[ID0]: SEC) (rcvTimeoutMsgCnt[ID0]: RTC) (rcvOkMsgCnt[ID0]: ROC)
     (procIds: IDS) (tran: ibl(ID0)) syncCurLeader(S, ID0, smallerThanIds(ID0, IDS))}
    if SEC > 0 and SEC == RTC and ROC == 0 .
endm

in model-checker .

mod BULLY-PREDS is
  pr BULLY .
  inc SATISFACTION .
  subsort BState < State .
  vars ID : Nat .
  var E : LabeledEvent .
  var S : Soup{OComp} .
  var PROP : Prop .
  op leader : Nat -> Prop .
  op enabled : LabeledEvent -> Prop .
  op applied : LabeledEvent -> Prop .
  eq {(proc[ID]: leader) S} |= leader(ID) = true .
  eq {(tran: bi(ID)) S} |= enabled(ibl(ID)) = true .
  eq {(tran: nee(ID)) S} |= enabled(ibl(ID)) = true .
  eq {(tran: E) S} |= applied(E) = true .
  eq {S} |= PROP = false [owise] .
endm

mod BULLY-CHECK is
  inc BULLY-PREDS .
  inc MODEL-CHECKER .
  inc LTL-SIMPLIFIER .
  var E : LabeledEvent .
  op leaderLiveness : -> Formula .
  op sf : LabeledEvent -> Formula .
  op sfair : -> Formula .
  eq leaderLiveness = <> (leader(0) \/ leader(1) \/ leader(2) \/ leader(3) \/ leader(4)) .
  eq sf(E) = ([] <> enabled(E)) -> ([] <> applied(E)) .
  eq sfair = sf(ibl(0)) /\ sf(ibl(1)) /\ sf(ibl(2)) /\ sf(ibl(3)) /\ sf(ibl(4)) .
endm

search [1] in BULLY : init =>* {(proc[ID0]: leader) (proc[ID1]: leader) S} .

red in BULLY-CHECK : modelCheck(init2, sfair -> leaderLiveness) .
